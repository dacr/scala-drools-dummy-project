package dummy;

dialect "mvel"

import java.util.LinkedList;
import dummy.model.*;

// --------------------------------------------------------------

rule "who is young"
  when
    Someone($name:name, age < 30)
  then
    System.out.println($name+" is very young")
end

// --------------------------------------------------------------

rule "people cars"
  when
    $car : Car($owner:someone.name)
  then
    System.out.println("One of "+$owner+"'s car is "+$car.color.name+" "+$car.model)
end

// --------------------------------------------------------------

rule "who own at least one red car"
  when
    $someone:Someone($name:name)
    exists Car(someone == $someone, color == Color.red)
  then
    System.out.println($name+" has at least one red car")
end

// --------------------------------------------------------------

rule "who doesn't have a home"
  when
    $someone:Someone($name:name)
    not Home(someone == $someone)
  then
    insert(new InformationRequest($someone, $name+"'s home is unknown"))
end

// --------------------------------------------------------------

rule "who doesn't have an address"
  when
    Home(
       $someone:someone,
       !address.isDefined)
  then
    insert(new InformationRequest($someone, $someone.name+" address is unknown"))
end

// --------------------------------------------------------------

rule "Who live at London"
  when
    Home(
      $someone:someone,
      address.isDefined,
      address.get.town == "London")
  then
    insert(new InformationRemarkable($someone, $someone.name+" lives in London"))
end

// --------------------------------------------------------------

rule "who owns more than 1 car"
  when
    $someone : Someone($name:name)
    $cars    : LinkedList( size>1 ) from collect(Car(someone == $someone))
  then
    insert(new InformationRemarkable($someone, $someone.name+" has "+$cars.size+" cars"))
end

// --------------------------------------------------------------

rule "process information requests"
  when
    InformationRequest($who:someone, $what:message)
  then
    System.out.println("INFORMATION REQUESTED FOR "+$who+" : "+$what)
end


rule "process remarkable information"
  when
    InformationRemarkable($who:someone, $what:info)
  then
    System.out.println("REMARKABLE INFORMATION ABOUT "+$who+" : "+$what)
end

// --------------------------------------------------------------

